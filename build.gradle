/*
 * Copyright (C) by MinterTeam. 2018
 * @link <a href="https://github.com/MinterTeam">Org Github</a>
 * @link <a href="https://github.com/edwardstock">Maintainer Github</a>
 *
 * The MIT License
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in
 * all copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
 * THE SOFTWARE.
 */

buildscript {
    repositories {
        google()
        jcenter()
        mavenLocal()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.0-rc02'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.+'
    }
}

allprojects {
    repositories {
        mavenLocal()
        google()
        jcenter()
        maven { url "https://dl.bintray.com/ethereum/maven/" }
        maven { url 'https://clojars.org/repo/' }
        maven { url 'https://jitpack.io' }
        maven { url "https://maven.edwardstock.com/artifactory/libs-release-local" }
        maven { url "https://repo1.maven.org/maven2/com/google/zxing/" }
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots/'}
    }
}

apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'jacoco'
apply plugin: 'com.jfrog.bintray'


group = 'network.minter.android'
version = '0.1.1'

ext {
    minterMinSdk = 16
    minterMaxSdk = 28
    minterBuildTools = "28.0.2"
    minterLibSupport = "27.1.1"
    minterCoreVers = "0.1.0"
    pomDescription = "Minter Android Blockchain API SDK"
}

def libPath = ""
if(hasProperty("nativeLibPath")) {
    libPath = getProperty("nativeLibPath")
}
println "Native lib path: ${libPath}"


android {
    compileSdkVersion minterMaxSdk
    buildToolsVersion minterBuildTools

    defaultConfig {
        minSdkVersion minterMinSdk
        targetSdkVersion minterMaxSdk

        versionCode 1
        versionName version

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    testOptions {
        unitTests {
            all {
                jvmArgs "-Djava.library.path=${libPath}"
            }
            includeAndroidResources = true
            returnDefaultValues = true
        }
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

    packagingOptions {
        exclude 'META-INF/spring.tooling'
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "env"

    productFlavors {
        // can't use name starts with 'test'
        netMain {
            dimension "env"
            //@TODO
            buildConfigField "String", "BASE_NODE_URL", '"http://minter-node-1.testnet.minter.network:8841"'
        }
        netTest {
            dimension "env"
            buildConfigField "String", "BASE_NODE_URL", '"http://minter-node-1.testnet.minter.network:8841"'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    netTestImplementation "network.minter.android:minter-android-core-testnet:${minterCoreVers}"
    netMainImplementation "network.minter.android:minter-android-core:${minterCoreVers}"


    implementation 'com.android.support:support-core-utils:27.1.1'
    // network
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.9.0'
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'

    implementation 'org.parceler:parceler-api:1.1.11'
    annotationProcessor 'org.parceler:parceler:1.1.11'

    // stuff
    implementation 'com.jakewharton.timber:timber:4.5.1'

    // testing
    testImplementation 'junit:junit:4.12'
    androidTestImplementation "com.android.support:support-annotations:${minterLibSupport}"
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    androidTestImplementation 'com.squareup.retrofit2:converter-gson:2.4.0'
    androidTestImplementation 'com.google.code.gson:gson:2.8.2'
    androidTestImplementation 'com.android.support:multidex:1.0.3'
}

// Coverage
apply from: 'scripts/jacoco.gradle'
// Publishing
apply from: 'scripts/minter_publish.gradle'
